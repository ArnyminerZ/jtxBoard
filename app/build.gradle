/*
 * Copyright (c) Techbee e.U.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'


android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "at.techbee.jtx"
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 100030006
        versionName "1.00.03-rc6"      // keep -release as a suffix also for release, build flavor adds the suffix e.g. .gplay (e.g. 1.00.00-rc0.gplay)
        buildConfigField "String", "versionCodename", "\"VaTacito\""
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // see https://developer.android.com/studio/write/java8-support
        kotlinOptions {
            jvmTarget = "1.8"
        }
        testOptions {
            unitTests.includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // uncomment to test minifyEnabled without signing the package
        debug {
            //minifyEnabled = true
            //shrinkResources = true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Enables data binding.
    buildFeatures {
        dataBinding true
        viewBinding true
    }

    //necessary for ical4j
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/*.md'

        pickFirst 'META-INF/AL2.0'
        pickFirst 'META-INF/LGPL2.1'
    }

    flavorDimensions 'version'
    productFlavors {
        gplay {
            dimension 'version'
            versionNameSuffix '.gplay'
        }
        huawei {
            dimension 'version'
            versionNameSuffix '.huawei'
        }
        ose {
            dimension 'version'
            versionNameSuffix '.ose'
        }
        global {
            dimension 'version'
            versionNameSuffix '.global'
        }
    }

    // the flavor alphatest should not be used for the build type release, we filter this here
    variantFilter { variant ->
        if (variant.flavors*.name.contains("alphatest") && variant.buildType.name == "release") {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs, this is especially necessary to use java.time for versions < O
        // https://developer.android.com/studio/write/java8-support#library-desugaring
        coreLibraryDesugaringEnabled = true
    }

    lintOptions{
        disable 'MissingTranslation'   // do not fail lint check because of missing translations
    }
}

aboutLibraries {
    additionalLicenses {
        mpl_2_0
    }
}

// defining the room.schemaLocation explicitly allows AutoMigrations
kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    implementation "androidx.constraintlayout:constraintlayout:$version_constraint_layout"

    // Android KTX
    implementation "androidx.core:core-ktx:$version_core"

    // Room and Lifecycle dependencies
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.appcompat:appcompat:$version_appcompat"
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'

    kapt "androidx.room:room-compiler:$version_room"
    implementation "androidx.lifecycle:lifecycle-extensions:$version_lifecycle_extensions"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$version_navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$version_navigation"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$version_lifecycle"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$version_room"

    // Material.io
    implementation "com.google.android.material:material:$version_material"

    implementation "androidx.preference:preference-ktx:1.1.1"

    // Volley for simple network requests
    implementation("com.android.volley:volley:1.2.1")

    // Admob & Google Consent for Admob & Google billing
    gplayImplementation "com.google.android.gms:play-services-ads:20.5.0"
    gplayImplementation "com.google.android.ump:user-messaging-platform:2.0.0"
    gplayImplementation "com.android.billingclient:billing-ktx:$version_billing"
    globalImplementation "com.google.android.gms:play-services-ads:20.5.0"
    globalImplementation "com.google.android.ump:user-messaging-platform:2.0.0"

    // Huawei Ads Lite (works only on Huawei phones)
    huaweiImplementation "com.huawei.hms:ads:3.4.49.301"    // should be replaced by ads-lite when it can be tested with a huawei phone
    //implementation "com.huawei.hms:ads-lite:13.4.49.301"
    huaweiImplementation "com.huawei.hms:ads-consent:3.4.49.301"

    // Workmanager support
    implementation("androidx.work:work-runtime-ktx:$version_work")

    //support for java.time for older Android versions (<O)
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0-alpha03'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Testing-only dependencies
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_kotlin"
    androidTestImplementation 'androidx.test:core-ktx:1.4.1-alpha03'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.room:room-testing:$version_room"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.test:core-ktx:1.4.1-alpha03'
    // Required -- JUnit 4 framework
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation("org.mockito:mockito-core:3.9.0")


    //Fragment Testing
    debugImplementation('androidx.fragment:fragment-testing:1.4.0') {
        // exclude androidx.test:core while fragment_testing depends on 1.1.0
        exclude group: 'androidx.test', module: 'core'
    }

    // Testing for Workmanager
    androidTestImplementation("androidx.work:work-testing:$version_work")

    // START packages for ical4j
    api("org.mnode.ical4j:ical4j:3.1.1") {
        exclude module: 'javax.mail'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
        exclude group: 'org.codehaus.groovy', module: 'groovy-dateutil'
    }
    // ical4j requires newer Apache Commons libraries, which require Java8. Force latest Java7 versions.
    // noinspection GradleDependency
    api("org.apache.commons:commons-collections4:4.2")
    // noinspection GradleDependency
    api("org.apache.commons:commons-lang3:3.8.1")

    // ical4j requires JavaMail API (e.g. for EMAIL parameter)
    implementation 'com.sun.mail:android-mail:1.6.7'

    // noinspection GradleDependency
    implementation "commons-io:commons-io:2.6"
    implementation "commons-codec:commons-codec:1.15"

    implementation "org.slf4j:slf4j-jdk14:1.7.32"

    // END packages for ical4j

    //About Libraries
    implementation "com.mikepenz:aboutlibraries:8.9.4"

}

